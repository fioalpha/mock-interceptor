plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven'
    id 'signing'
}

group = "com.github.gustafah"
archivesBaseName = "mock-interceptor"
version = application.versionName

signing {
    sign configurations.archives
}

android {
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        excludes = ['**/*.kt']
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Mock Interceptor'
                    packaging 'jar'
                    description 'An interceptor for Mocking Requests :)'
                    url 'https://github.com/gustafah/mock-interceptor'

                    scm {
                        url 'https://github.com/gustafah/mock-interceptor'
                        connection "scm:https://github.com/gustafah/mock-interceptor.git"
                        developerConnection 'scm:git@github.com:gustafah/mock-interceptor.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id developerId
                            name developerName
                            email developerEmail
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation dependency_versions.kotlin
    implementation dependency_versions.coreKtx
    implementation dependency_versions.appCompat

    implementation 'com.squareup.okhttp3:okhttp:4.0.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'org.threeten:threetenbp:1.5.0:no-tzdb'
}